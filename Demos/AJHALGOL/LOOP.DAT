on
telecode 903
at ptr file ALG16KLG(AJH).BIN
j 8181
st 8175

at ptr inline
loop;
"begin"
   "comment" 13-JAN-2001 with plotting. 
       Path of a loop where there is no air resistance;
   "real" dt, eps, eta, g, k, pi, t, tend, theta, v;
   "array" y, y2, z[1:4];

   "procedure" fun(x, y, n, z); "value" x,n; "real" x;
      "array" y, z; "integer" n;
   "begin"
      z[1] := y[3];
      z[2] := y[4];
      theta := ("if" abs(y[3]) < 0.001 
                "then" (pi/2) * sign(y[4])
                "else"
                  ("if" y[3] < 0 
                   "then" pi + arctan(y[4] / y[3])
                   "else" arctan(y[4] / y[3])));
      z[3] := -k * g * sin(theta);
      z[4] := k * g * cos(theta) - g
   "end" of fun;

   "procedure" rk(x,y,n,fkt,eps,eta,xe,ye,fi);
      "value" x,n,eps,eta,xe,fi;
      "integer" n; "boolean" fi;
      "real" x,eps,eta,xe; "array" y,ye;
      "procedure" fkt;

   "comment" Algorithm 9 in Collections CACM, also see
             example in Algol Report 1960. Note that this
             version is in a single-case alphabet, and that
             as many scalar parameters as possible are
             called by value;

   "begin"
      "array" z,y1,y2,y3[1:n];
      "real" x1,x2,x3,h,hs;
      "integer" k,j,s;
      "boolean" out;

      "comment" s and hs are supposed to be own variables, but
                in 903 Algol all locals are treated as own. Also
                note that s has been made integer as Thacher 
                recommends;

      "real" "procedure" comp(a,b,c);
         "value" a,b,c;
         "real"  a,b,c;

      "comment" This is from H.C. Thacher's certification
                of Algorithm 9, July 1964, Nov. 1965. It
                should be in assembly code;
      "begin"
         "integer" ae,be,ce;

         "integer" "procedure" expon(x);
            "value" x;
            "real"  x;
            expon := "if" x=0 
                        "then" -22
                        "else" entier(0.43429448*ln(abs(x))+1);

            "comment" -22 is smaller than -18, the exponent of
                      the smallest no. in 903 Algol (decimal
                      exponents);

         ae := expon(a);
         be := expon(b);
         ce := expon(c);
         "if" ae < be "then" ae := be;
         "if" ae < ce "then" ae := ce;
         comp := abs(a-b)/10^ae
      "end" of comp;

      "procedure" rk1st(x,y,h,xe,ye);
         "value" x,h;
         "real" x,h,xe; "array" y,ye;
      "begin"
         "array" w[1:n],a[1:5];
         "integer" k,j;
         a[1] := a[2] := a[5] := h/2;
         a[3] := a[4] := h;
         xe := x;
         "for" k := 1 "step" 1 "until" n "do" 
                         ye[k] := w[k] := y[k];
         "for" j := 1 "step" 1 "until" 4 "do"
         "begin"
            "comment" In 903 Algol there is a local convention -
                      in formal procedures like fkt the parameters
                      called by value, ie xe and n, must be 
                      surrounded by brackets, and the ones
                      called by name, ie w and z, must not be; 
            fkt((xe),w,(n),z);
            xe := x + a[j];
            "for" k := 1 "step" 1 "until" n "do"
            "begin"
               w[k]  := y[k]  + a[j]   * z[k];
               ye[k] := ye[k] + a[j+1] * z[k] /3
            "end" of k
         "end" of j
      "end" of rk1st;

      "comment" start of rk;

      "if" fi "then"
      "begin"
         h := xe - x;
         s := 0
      "end" "else" h := hs;

      out := "false";
AA:
      "if" (x + 2.01 * h - xe > 0) "equiv" (h > 0) "then"
      "begin"
         hs := h;
         out := "true";
         h := (xe - x) / 2
      "end" of if;

      rk1st(x,y,2*h,x1,y1);
BB:
      rk1st(x,y,h,x2,y2);
      rk1st(x2,y2,h,x3,y3);
      "for" k := 1 "step" 1 "until" n "do"
         "if" comp(y1[k],y3[k],eta) > eps "then" "goto" CC;
      x := x3;
      "if" out "then" "goto" DD;

      "for" k := 1 "step" 1 "until" n "do" y[k] := y3[k];

      "if" s = 5 "then"
      "begin"
         s := 0;
         h := 2 * h
      "end";
      s := s + 1; 
      "goto" AA;
CC:
      h := 0.5 * h;
      out := "false";
      x1 := x2;
      "for" k := 1 "step" 1 "until" n "do" y1[k] := y2[k];
      "goto" BB;
DD:
      "for" k := 1 "step" 1 "until" n "do" ye[k] := y3[k]
   "end" of RK, SEP-78, DGNH;

   "comment" program start;
   "print" ''L`31-OCT-2000`;

   g := 10;
   pi := 4 * arctan(1);
   "print" ''L`vel (m/s), no. of g, duration, interval, eps, eta`;

   "read" v, k, tend, dt, eps, eta;
   "print" v, same line, k, tend, dt, eps, eta;
   set origin(50,300);
   move pen(0,-40); draw line(580,-40);
   move pen(180,-30); way(0,2); "print" punch(5), 'LOOP -`, same line,
   aligned(1,1), k, 'G`;
   move pen(0,0);
   t := y[1] := y[2] := y[4] := 0;
   y[3] := v;
loop:
   rk(t, y, 4, fun, eps, eta, t+dt, y2, t=0);
   y[1] := y2[1];
   y[2] := y2[2];
   y[3] := y2[3];
   y[4] := y2[4];
   t := t + dt;
   draw line(y[1]*2.8, y[2]*2.8);
   "if" t > tend "then" stop "else" "goto" loop
"end"
<!!>
k 12
j
st 1715
at ptr file LIBRARY.RLB
k 9
j
st 8000
at ptr inline
40  2  10  0.1  0.1  0.1
<!!>
at plt
k 10
select out tty
j
1715
de plt
sh t
