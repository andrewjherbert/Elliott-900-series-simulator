   OPASC
   IPBIN
   ADLIB
   JUMP 12
   IPASC
coords;
"begin"
   "comment" 10-JUL-1999;
   "real" alp, galp, lat, lon, palp, plat, plon;
   "procedure" convert(ilat, ilon, olat, olon, to polar);
      "value" ilat, ilon, to polar;
      "real"  ilat, ilon, olat, olon; "boolean" to polar;

      "comment" Refers to non-locals alp, galp, palp.
                convert always sets galp.
                to polar causes   palp := alp + galp.
                from polar causes alp  := palp + galp;
   "begin"
      "real" halfpi, pi, twopi, eps;
      "real" "procedure" plus(a)and:(b); "value" a, b; "real" a, b;
         plus := "if" a+b > pi-eps "then" a+b-twopi
                                   "else"
                 "if" a+b < -pi+eps "then" a+b+twopi
                                   "else" a+b;
      "real" "procedure" minus(a)and:(b); "value" a, b; "real" a, b;
         minus := "if" a-b > pi-eps "then" a-b-twopi
                                    "else"
                  "if" a-b < -pi+eps "then" a-b+twopi
                                    "else" a-b;
      "real" "procedure" atan2(a)over:(b); "value" a, b; "real" a, b;
         atan2 := "if" abs(b) < eps "then" halfpi*sign(a)
                                    "else" arctan(a/b);

      "procedure" polar(lat, lon, plat, plon);
         "value" lat, lon; "real" lat, lon, plat, plon;
      "begin"
         "real" clat, clon, slat, slon, clatslon;
         clat := cos(lat); clon := cos(lon);
         slat := sin(lat); slon := sin(lon);
         clatslon := clat * slon;
         plat := atan2(clat * clon, sqrt(clatslon^2 + slat^2));
         plon := atan2(slat, clatslon)    - halfpi*sign(lon);
         galp := atan2(slat * clon, slon) + halfpi*sign(lon)
      "end" of polar;

      "comment" convert starts here;

      halfpi := 2*arctan(1); pi := 2*halfpi;
      twopi := 2*pi; eps := 3?-8;
      "if" to polar "then"
      "begin"
         polar(ilat, minus(ilon)reduced by:(pi), olat, olon);
         palp := plus(galp)increased by:(alp)
      "end"
                    "else"
      "begin"
         polar(ilat, ilon, olat, olon);
         olon := plus(olon)increades by:(pi);
         alp := plus(galp)increased by:(palp)
      "end"
   "end" of convert;

   "real" c;
   c := 45/arctan(1);
   "print" ''L@10-JUL-1999@;
   "print" ''LS6@LAT'S9@LON'S8@PLAT'S8@PLON'S8@GALP@;
again:
   "read" lat, lon;
   "if" lat = 0 "and" lon = 0 "then" stop;
   alp := 0;
   convert(lat/c, lon/c, plat, plon, "true");
   "print" lat, same line, '   @, lon, '   @, plat*c, '   @, plon*c,
           '   @, galp*c;
   convert(plat, plon, lat, lon, "false");
   "print" lat*c, same line, '   @, lon*c, ''S27@@, alp*c;
   "goto" again
"end"
 65 -35 65 -125 65 55 65 145
 -65 -35 -65 -125 -65 55 -65 145
 90 0 0 -90 0 90 -90 0
 52.034  -0.131
 52.034   0.131
 51.5     0.1
 0 0

