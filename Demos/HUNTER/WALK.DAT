on
telecode 903
at ptr file ALG16KLG(HUNTER).BIN
j 8181
st 5124

at ptr inline
walk;
"begin"
   "comment" 13-JAN-2001;

   "integer" j, m, n,posx, posy, step, x, y, z; "real" val;
   "real" "procedure" Wichmann;
   "begin"
      "comment" The Wichmann-Hill portable random number
                generator. B. A. Wichmann and I. D. Hill,
                An Efficient and Portable Pseudo-Random Number
                Generator, Applied Statistics, 31 (No. 2),
                188-190 (1982) ;
      "real" w;
      x := 171 * (x - (x "div" 177) * 177) -  2 * (x "div" 177);
      y := 172 * (y - (y "div" 176) * 176) - 35 * (y "div" 176);
      z := 170 * (z - (z "div" 178) * 178) - 63 * (z "div" 178);
      "if" x < 0 "then" x := x + 30269;
      "if" y < 0 "then" y := y + 30307;
      "if" z < 0 "then" z := z + 30323;
      w := x/30269.0 + y/30307.0 + z/30323.0;
      Wichmann := w - entier(w) 
   "end" of Wichmann;


   "comment" program start;   "read" step, x, y, z;


   set origin(320,40); posx:=posy:=0;
   "for" j := 1 "step" 1 "until" 1000 "do"
   "begin"
      val := wichmann;
      "if" val > 0.0 "and" val < 0.25 "then" posx := posx+step  "else"
      "if" val >0.25 "and" val < 0.5  "then" posy := posy+step  "else"
      "if" val > 0.5 "and" val < 0.75 "then" posx := posx -step "else"
      posy := posy - step;
      draw line(posx, posy)
   "end"

"end"
<!!>
k 12
j
st 1727
at ptr file LIBRARY.RLB
k 9
j
st 7943
at ptr inline

  8 123  67  83
<!!>
at plt
select out tty
k 10
j
de plt
